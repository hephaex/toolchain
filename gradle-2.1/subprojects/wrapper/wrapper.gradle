/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
dependencies {
    compile project(":cli")

    testCompile libraries.groovy
    testCompile libraries.ant

    integTestRuntime rootProject.configurations.testRuntime.allDependencies
}

task buildReceiptResource(type: Copy, dependsOn: rootProject.createBuildReceipt) {
    into generatedTestResourcesDir
    from rootProject.createBuildReceipt.receiptFile
}
sourceSets.test.output.dir generatedTestResourcesDir, builtBy: buildReceiptResource

task executableJar(type: Jar) {
    archiveName = 'gradle-wrapper.jar'
    from sourceSets.main.output
    from rootProject.createBuildReceipt
    from configurations.runtime.allDependencies.withType(ProjectDependency).collect { it.dependencyProject.sourceSets.main.output }
}

integTestTasks.all {
    inputs.files { project(":distributions").binZip }
}

useTestFixtures()
useClassycle()
